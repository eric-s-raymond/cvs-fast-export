apply plugin: 'scala'
apply plugin: 'application'

mainClassName = 'org.rvaughn.scm.cvs.FastExport'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:4.11'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.9.3'
    compile 'org.sellmerfud:optparse_2.9.1:1.0'
}

jar {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

task bundle(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    description 'Assembles a runnable jar archive containing all required classes.'
    group 'build'
    ext.outputFile = file("${libsDir}/${project.name}-bundle.jar")
    injars jar.archivePath
    injars configurations.runtime.files, filter: '!META-INF/MANIFEST.MF'
    outjars outputFile
    configuration 'proguard.cfg'
}

wrapper {
    gradleVersion = '1.12'
}
